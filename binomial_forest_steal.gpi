set terminal png enhanced font 'Helvetica,18' linewidth 2

set style line 1 lt 1 lc 1 lw 2 pt  5 ps 2
set style line 2 lt 1 lc 2 lw 2 pt  7 ps 2
set style line 3 lt 1 lc 3 lw 2 pt  9 ps 2
set style line 4 lt 1 lc 4 lw 2 pt 11 ps 2
set style line 5 lt 1 lc 5 lw 2 pt 11 ps 2
set style line 6 lt 1 lc 6 lw 2 pt 11 ps 2

# Sensible colors
load "~/github/gnuplot-colorbrewer/qualitative/Set1.plt"

set key top left
set key font "Helvetica,14"
set key spacing 1.2
set key width -4
set key samplen 2

set xlabel "Total number of nodes"
set ylabel "Number of stolen nodes"
set xrange [1:10**3]
set yrange [1:10**3]
set xtics 0, 200
set ytics 0, 200
set mxtics 2
set mytics 2
set grid

set size square

min(x, y) = (x < y) ? x : y
max(x, y) = (x > y) ? x : y
log2(x) = log10(x) / log10(2)
is_power_of_2(x) = (x == 1) ? 1 : ((int(x) % 2 == 0) ? is_power_of_2(int(x/2)) : 0)
round(x) = (x - floor(x) < 0.5) ? floor(x) : ceil(x)

# Hypothetical work-stealing strategies
steal_quarter(x) = max(1, floor(x/4))
steal_third(x) = max(1, floor(x/3))
steal_two_thirds(x) = max(1, floor(2 * x/3))
steal_all(x) = x

# Steal-half deque
steal_half(x) = max(1, floor(x/2))

# (1) Steal leftmost child of highest-order binomial tree
binomial_forest_steal_1(x) = 2 ** (floor(log2(x)) - 1)

# (2) Steal complete highest-order binomial tree
binomial_forest_steal_2(x) = 2 ** (ceil(log2(x)) - 1)

# (3) Combining (1) and (2)
binomial_forest_steal_3(x) = (x > 1.5 * 2 ** floor(log2(x))) ? binomial_forest_steal_2(x) : binomial_forest_steal_1(x)

set output "binomial_forest_steal_1.png"

plot steal_quarter(x) w l ls 1 t "Steal 1/4", \
     steal_half(x) w l ls 2 t "Steal 1/2", \
     binomial_forest_steal_1(x) w l ls 3 t "binomial\\_forest\\_steal\\_1"

set output "binomial_forest_steal_2.png"

plot steal_half(x) w l ls 1 t "Steal 1/2", \
     steal_all(x) w l ls 2 t "Steal all", \
     binomial_forest_steal_2(x) w l ls 3 t "binomial\\_forest\\_steal\\_2"

set output "binomial_forest_steal_3.png"

plot steal_third(x) w l ls 1 t "Steal 1/3", \
     steal_two_thirds(x) w l ls 2 t "Steal 2/3", \
     binomial_forest_steal_3(x) w l ls 3 t "binomial\\_forest\\_steal\\_3"
